set serveroutput on;
/
DECLARE
CURSOR C_RegularizaContratos
IS
SELECT CLIENTES.CANTIDAD_SERV AS CANT_SERV_CANCEL,
  CLIENTES_TOTAL.CANTIDAD_SERV_TOTAL AS CANT_SERV_TOTAL,
  TROL.DESCRIPCION_TIPO_ROL,
  PER.ID_PERSONA,
  OFI.NOMBRE_OFICINA AS OFICINA_FACTURACION,
  PER.NOMBRES,
  PER.APELLIDOS,
  PER.RAZON_SOCIAL,
  PER.REPRESENTANTE_LEGAL,
  PER.TIPO_IDENTIFICACION,
  PER.IDENTIFICACION_CLIENTE, 
  PEMPROL.ID_PERSONA_ROL,
  PER.FE_CREACION,
  PER.USR_CREACION, 
  PEMPROL.ESTADO AS ESTADO_PEMPROL,
  NVL(DB_COMERCIAL.CMKG_REPORTE_APROB_CONTRATOS.F_INFORMACION_CONTRATO_CLI('ID_CONTRATO',PEMPROL.ID_PERSONA_ROL,PEMPROL.ESTADO),' ') AS ID_CONTRATO,
  NVL(DB_COMERCIAL.CMKG_REPORTE_APROB_CONTRATOS.F_INFORMACION_CONTRATO_CLI('ESTADO',PEMPROL.ID_PERSONA_ROL,PEMPROL.ESTADO),' ') AS ESTADO_CONTRATO
FROM DB_COMERCIAL.INFO_PERSONA PER
--
JOIN
  (SELECT PER.IDENTIFICACION_CLIENTE,
    COUNT(*) CANTIDAD_SERV
  FROM DB_COMERCIAL.INFO_PERSONA PER,
  DB_COMERCIAL.INFO_PERSONA_EMPRESA_ROL PEMPROL,
  DB_COMERCIAL.INFO_PUNTO PTO,
  DB_COMERCIAL.INFO_SERVICIO SERV,
  DB_COMERCIAL.INFO_EMPRESA_ROL EMPROL,
  DB_COMERCIAL.INFO_EMPRESA_GRUPO EMPGRUP,
  DB_GENERAL.ADMI_ROL ROL,
  DB_GENERAL.ADMI_TIPO_ROL TROL,
  DB_COMERCIAL.INFO_OFICINA_GRUPO OFI  
  WHERE 
  PER.ID_PERSONA                = PEMPROL.PERSONA_ID 
  AND PEMPROL.ID_PERSONA_ROL    = PTO.PERSONA_EMPRESA_ROL_ID
  AND PEMPROL.EMPRESA_ROL_ID    = EMPROL.ID_EMPRESA_ROL
  AND EMPROL.EMPRESA_COD        = EMPGRUP.COD_EMPRESA
  AND EMPGRUP.PREFIJO           = 'TN'
  AND EMPROL.ROL_ID             = ROL.ID_ROL
  AND ROL.TIPO_ROL_ID           = TROL.ID_TIPO_ROL
  AND TROL.DESCRIPCION_TIPO_ROL = 'Cliente'
  AND PEMPROL.OFICINA_ID        = OFI.ID_OFICINA
  AND SERV.PUNTO_ID             = PTO.ID_PUNTO
  AND SERV.ESTADO IN ('Cancel','Cancel-SinEje','Anulado','Anulada','Eliminada','Eliminado','Trasladado','Rechazada','Eliminado-Migra')  
  GROUP BY PER.IDENTIFICACION_CLIENTE
  ) CLIENTES ON CLIENTES.IDENTIFICACION_CLIENTE=PER.IDENTIFICACION_CLIENTE
 --
JOIN
  (SELECT PER.IDENTIFICACION_CLIENTE,
    COUNT(*) CANTIDAD_SERV_TOTAL
  FROM DB_COMERCIAL.INFO_PERSONA PER,
  DB_COMERCIAL.INFO_PERSONA_EMPRESA_ROL PEMPROL,
  DB_COMERCIAL.INFO_PUNTO PTO,
  DB_COMERCIAL.INFO_SERVICIO SERV,
  DB_COMERCIAL.INFO_EMPRESA_ROL EMPROL,
  DB_COMERCIAL.INFO_EMPRESA_GRUPO EMPGRUP,
  DB_GENERAL.ADMI_ROL ROL,
  DB_GENERAL.ADMI_TIPO_ROL TROL,
  DB_COMERCIAL.INFO_OFICINA_GRUPO OFI  
  WHERE 
  PER.ID_PERSONA                = PEMPROL.PERSONA_ID 
  AND PEMPROL.ID_PERSONA_ROL    = PTO.PERSONA_EMPRESA_ROL_ID
  AND PEMPROL.EMPRESA_ROL_ID    = EMPROL.ID_EMPRESA_ROL
  AND EMPROL.EMPRESA_COD        = EMPGRUP.COD_EMPRESA
  AND EMPGRUP.PREFIJO           = 'TN'
  AND EMPROL.ROL_ID             = ROL.ID_ROL
  AND ROL.TIPO_ROL_ID           = TROL.ID_TIPO_ROL
  AND TROL.DESCRIPCION_TIPO_ROL = 'Cliente'
  AND PEMPROL.OFICINA_ID        = OFI.ID_OFICINA
  AND SERV.PUNTO_ID             = PTO.ID_PUNTO     
  GROUP BY PER.IDENTIFICACION_CLIENTE
  ) CLIENTES_TOTAL ON CLIENTES_TOTAL.IDENTIFICACION_CLIENTE=PER.IDENTIFICACION_CLIENTE
 --
JOIN DB_COMERCIAL.INFO_PERSONA_EMPRESA_ROL PEMPROL
ON PER.ID_PERSONA =PEMPROL.PERSONA_ID
JOIN DB_COMERCIAL.INFO_EMPRESA_ROL EMPROL
ON PEMPROL.EMPRESA_ROL_ID=EMPROL.ID_EMPRESA_ROL
JOIN DB_COMERCIAL.INFO_EMPRESA_GRUPO EMPGRUP
ON EMPROL.EMPRESA_COD=EMPGRUP.COD_EMPRESA 
JOIN DB_GENERAL.ADMI_ROL ROL 
ON EMPROL.ROL_ID=ROL.ID_ROL
JOIN DB_GENERAL.ADMI_TIPO_ROL TROL
ON ROL.TIPO_ROL_ID  =TROL.ID_TIPO_ROL 
JOIN DB_COMERCIAL.INFO_OFICINA_GRUPO OFI 
ON PEMPROL.OFICINA_ID=OFI.ID_OFICINA 
WHERE
EMPGRUP.PREFIJO               = 'TN' 
AND TROL.DESCRIPCION_TIPO_ROL = 'Cliente'
AND CLIENTES.CANTIDAD_SERV    = CLIENTES_TOTAL.CANTIDAD_SERV_TOTAL 
AND NVL(DB_COMERCIAL.CMKG_REPORTE_APROB_CONTRATOS.F_INFORMACION_CONTRATO_CLI('ESTADO',PEMPROL.ID_PERSONA_ROL,PEMPROL.ESTADO),' ') NOT IN ('Anulado','Cancelado','Rechazado')
AND EXISTS
    (SELECT 1
    FROM DB_COMERCIAL.INFO_SERVICIO SERV,
    DB_COMERCIAL.INFO_PUNTO PTO
    WHERE SERV.PUNTO_ID            = PTO.ID_PUNTO
    AND PTO.PERSONA_EMPRESA_ROL_ID = PEMPROL.ID_PERSONA_ROL 
    AND SERV.ESTADO                IN ('Cancel')
    )
;

CURSOR C_PuntosRegulariza(Cn_IdPersonaRol DB_COMERCIAL.INFO_PERSONA_EMPRESA_ROL.ID_PERSONA_ROL%TYPE)
IS
SELECT ID_PUNTO, ESTADO,PERSONA_EMPRESA_ROL_ID 
FROM DB_COMERCIAL.INFO_PUNTO 
WHERE 
ESTADO NOT IN ('Anulado','Cancelado','Eliminado','Trasladado')
AND PERSONA_EMPRESA_ROL_ID =Cn_IdPersonaRol;

CURSOR C_ServiciosPorPunto(Cn_IdPunto DB_COMERCIAL.INFO_PUNTO.ID_PUNTO%TYPE)
IS
SELECT COUNT(*) AS CANTIDAD_SERV_CANCEL
FROM DB_COMERCIAL.INFO_SERVICIO 
WHERE PUNTO_ID = Cn_IdPunto
AND ESTADO IN ('Cancel','Cancel-SinEje');

 Ln_IdPersonaEmpRolHisto DB_COMERCIAL.INFO_PERSONA_EMPRESA_ROL_HISTO.ID_PERSONA_EMPRESA_ROL_HISTO%TYPE;
 Ln_IdPuntoHistorial     DB_COMERCIAL.INFO_PUNTO_HISTORIAL.ID_PUNTO_HISTORIAL%TYPE;
 Ln_CantClientesAct      NUMBER          := 0;
 Ln_CantPuntosAct        NUMBER          := 0;
 Ln_CantContratosAct     NUMBER          := 0;
 Lv_EstadoPunto          DB_COMERCIAL.INFO_PUNTO.ESTADO%TYPE;
 Ln_CantidadServCancel   NUMBER          := 0;

BEGIN  
  SYS.DBMS_OUTPUT.PUT_LINE('----------------------------------------------------------');    
  SYS.DBMS_OUTPUT.PUT_LINE('Fecha/Hora Inicio: '|| TO_CHAR(sysdate, 'YYYY/MM/DD HH24:MI:SS') );    
  SYS.DBMS_OUTPUT.PUT_LINE('----------------------------------------------------------');   
  --   
 IF C_ServiciosPorPunto%ISOPEN THEN  
   CLOSE C_ServiciosPorPunto;       
 END IF;
--
FOR Lr_RegularizaContratos IN C_RegularizaContratos LOOP                
       
      --Inserto Historial a nivel del Cliente Regularizado
      Ln_IdPersonaEmpRolHisto := DB_COMERCIAL.SEQ_INFO_PERSONA_EMPRESA_ROL_H.NEXTVAL;
      INSERT INTO 
      DB_COMERCIAL.INFO_PERSONA_EMPRESA_ROL_HISTO
      (ID_PERSONA_EMPRESA_ROL_HISTO,
       USR_CREACION,
       FE_CREACION,
       IP_CREACION,
       ESTADO,
       PERSONA_EMPRESA_ROL_ID,
       OBSERVACION,
       MOTIVO_ID) 
      VALUES
      (Ln_IdPersonaEmpRolHisto,
       'telcos_regula',
       sysdate,
       '127.0.0.1',
       'Cancelado',
       Lr_RegularizaContratos.ID_PERSONA_ROL,
       '(ESTADO) ANTERIOR:'|| Lr_RegularizaContratos.ESTADO_PEMPROL ||' - ACTUAL:'|| 'Cancelado',
       1512);

     --Actualizo Estado del cliente y guardo Historial
      UPDATE DB_COMERCIAL.INFO_PERSONA_EMPRESA_ROL SET ESTADO='Cancelado' 
      WHERE ID_PERSONA_ROL=Lr_RegularizaContratos.ID_PERSONA_ROL;

      Ln_CantClientesAct := Ln_CantClientesAct + 1;

     --Actualizo Estado del Contrato a Cancelado
      UPDATE DB_COMERCIAL.INFO_CONTRATO SET ESTADO='Cancelado' 
      WHERE ID_CONTRATO=Lr_RegularizaContratos.ID_CONTRATO;
      
      Ln_CantContratosAct := Ln_CantContratosAct + 1;

     FOR Lr_PuntosRegulariza IN C_PuntosRegulariza(Lr_RegularizaContratos.ID_PERSONA_ROL) LOOP                
     
       OPEN C_ServiciosPorPunto(Lr_PuntosRegulariza.ID_PUNTO);
     
       FETCH C_ServiciosPorPunto INTO Ln_CantidadServCancel;
     
       CLOSE C_ServiciosPorPunto;
       --
       IF Ln_CantidadServCancel >0 THEN
         Lv_EstadoPunto :='Cancelado';
       ELSE 
         Lv_EstadoPunto :='Anulado';
       END IF;

       --Actualizo Estado del Punto a Cancelado
       Ln_IdPuntoHistorial := DB_COMERCIAL.SEQ_INFO_PUNTO_HISTORIAL.NEXTVAL;
       --
       INSERT INTO DB_COMERCIAL.INFO_PUNTO_HISTORIAL 
       (ID_PUNTO_HISTORIAL,
        PUNTO_ID,
        VALOR,
        ACCION,
        USR_CREACION,
        FE_CREACION,
        IP_CREACION)
       VALUES
       (Ln_IdPuntoHistorial,
       Lr_PuntosRegulariza.ID_PUNTO,
       '(ESTADO_PUNTO) ANTERIOR: '|| Lr_PuntosRegulariza.ESTADO ||' - ACTUAL:' || Lv_EstadoPunto,
       'RegularizaEstadosPorCancelacion',
       'telcos_regula',
       SYSDATE,
       '127.0.0.1');

        UPDATE DB_COMERCIAL.INFO_PUNTO
        SET ESTADO=Lv_EstadoPunto 
        WHERE ID_PUNTO=Lr_PuntosRegulariza.ID_PUNTO;

        Ln_CantPuntosAct:= Ln_CantPuntosAct + 1;
     END LOOP;
END LOOP;

 COMMIT;             

SYS.DBMS_OUTPUT.PUT_LINE('----------------------------------------------------------');    
SYS.DBMS_OUTPUT.PUT_LINE('Fecha/Hora Fin: '|| TO_CHAR(sysdate, 'YYYY/MM/DD HH24:MI:SS') );    
SYS.DBMS_OUTPUT.PUT_LINE('----------------------------------------------------------');  
SYS.DBMS_OUTPUT.PUT_LINE('CANTIDAD DE CLIENTES ACTUALIZADOS   : ' || Ln_CantClientesAct);
SYS.DBMS_OUTPUT.PUT_LINE('CANTIDAD DE PUNTOS ACTUALIZADOS     : ' || Ln_CantPuntosAct);
SYS.DBMS_OUTPUT.PUT_LINE('CANTIDAD DE CONTRATOS ACTUALIZADOS  : ' || Ln_CantContratosAct);

EXCEPTION  
  WHEN OTHERS THEN
     IF C_ServiciosPorPunto%ISOPEN THEN  
       CLOSE C_ServiciosPorPunto;       
     END IF;
     ROLLBACK;     
    SYS.DBMS_OUTPUT.PUT_LINE('Error en Regularizaci√≥n de Estados de Clientes, Puntos y Contratos: ' || SQLCODE || ' - ' || SQLERRM);
    
END;       


